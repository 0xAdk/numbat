### Dimensions

dimension Scalar = 1

dimension Length
dimension Area = Length^2
dimension Volume = Length^3

dimension Time
dimension Frequency = Time^(-1)
dimension Speed = Length / Time
dimension Acceleration = Length / Time^2

dimension Mass
dimension Momentum = Mass * Speed
dimension Force = Mass * Acceleration = Momentum / Time
dimension Energy = Momentum^2 / Mass = Mass * Speed^2 = Force * Length
dimension Power = Energy / Time = Force * Speed
dimension Pressure = Force / Area = Energy / Volume
dimension Action = Energy * Time
dimension MassDensity = Mass / Length^3

dimension Current
dimension Charge = Current * Time
dimension Voltage = Energy / Charge = Power / Current
dimension Capacitance = Charge / Voltage
dimension Resistance = Voltage / Current

dimension Temperature

### SI Units

unit meter: Length
unit second: Time
unit kilogram: Mass

unit hertz: Frequency = 1 / second
unit newton: Force = kilogram meter / second^2
unit joule: Energy = newton meter
unit watt: Power = joule / second
unit pascal: Pressure = newton / meter^2

unit ampere: Current
unit coulomb: Charge = ampere second
unit volt: Voltage = kilogram  meter^2 / (second^3 ampere)
unit farad: Capacitance = coulomb / volt
unit ohm: Resistance = volt / ampere

unit kelvin : Temperature

### Time units

unit minute: Time = 60 second
unit hour: Time = 60 minute
unit day: Time = 24 hour
unit year: Time = 365.2425 day
unit month: Time = year / 12

### Short versions of units (TODO)

let m = meter
let s = second
let kg = kilogram
let Hz = hertz
let N = newton
let J = joule
let W = watt
let P = pascal
let A = ampere
let C = coulomb
let V = volt
let F = farad
let Ω = ohm
let K = kelvin

let min = minute

### Prefixes light (TODO)

let milli = 0.001
let kilo = 1000

### Imperial units

unit foot: Length = 0.3048 meter

### Mathematical constants

let pi = 3.14159265358979323846264338327950288
let π = pi
let e = 2.71828182845904523536028747135266250
let ½ = 1/2
let ¼ = 1/4

### Physical constants

let gravity = 9.80665 meter / second^2
let speed_of_light = 299792458 meter / second

## Mathematical functions

# TODO: implement these as foreign functions?

fn sqrt<D>(x: D^2) -> D = x^(1/2)
fn sqr<D>(x: D) -> D^2 = x^2
fn exp(x: Scalar) -> Scalar = e^x

# Foreign functions

fn abs<T>(x: T) -> T
fn sin(x: Scalar) -> Scalar
fn atan2<T>(y: T, x: T) -> Scalar
