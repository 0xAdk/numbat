### Mathematical constants

let pi = 3.14159265358979323846264338327950288
let π = pi
let e = 2.71828182845904523536028747135266250
let ½ = 1/2
let ¼ = 1/4

### Physical dimensions

dimension Scalar = 1

dimension Length
dimension Area = Length^2
dimension Volume = Length^3

dimension Time
dimension Frequency = Time^(-1)
dimension Speed = Length / Time
dimension Acceleration = Length / Time^2

dimension Mass
dimension Momentum = Mass * Speed
dimension Force = Mass * Acceleration = Momentum / Time
dimension Energy = Momentum^2 / Mass = Mass * Speed^2 = Force * Length
dimension Power = Energy / Time = Force * Speed
dimension Pressure = Force / Area = Energy / Volume
dimension Action = Energy * Time
dimension MassDensity = Mass / Length^3

dimension Current
dimension Charge = Current * Time
dimension Voltage = Energy / Charge = Power / Current
dimension Capacitance = Charge / Voltage
dimension Resistance = Voltage / Current

dimension Temperature

### SI Units

@prefixes(decimal)
unit meter: Length

@prefixes(decimal)
unit second: Time

@prefixes(decimal)
unit gram: Mass

@prefixes(decimal)
unit hertz: Frequency = 1 / second

@prefixes(decimal)
unit newton: Force = kilogram meter / second^2

@prefixes(decimal)
unit joule: Energy = newton meter

@prefixes(decimal)
unit watt: Power = joule / second

@prefixes(decimal)
unit pascal: Pressure = newton / meter^2

@prefixes(decimal)
unit ampere: Current

@prefixes(decimal)
unit coulomb: Charge = ampere second

@prefixes(decimal)
unit volt: Voltage = kilogram  meter^2 / (second^3 ampere)

@prefixes(decimal)
unit farad: Capacitance = coulomb / volt

@prefixes(decimal)
unit ohm: Resistance = volt / ampere

@prefixes(decimal)
unit kelvin : Temperature

## SI accepted units

@prefixes(decimal)
unit radian = meter/meter # TODO

unit degree = π radian / 180

### Time units

unit minute: Time = 60 second
unit hour: Time = 60 minute
unit day: Time = 24 hour
unit year: Time = 365.2425 day
unit month: Time = year / 12

### Other units

dimension Bit
unit bit: Bit
unit byte: Bit = 8 bit

### Imperial units

unit inch: Length = 0.0254 meter

unit foot: Length = 0.3048 meter
assert_eq(1 foot, 12 inch, 0.001 inch)

unit yard: Length = 0.9144 meter
assert_eq(1 yard, 3 foot, 0.001 foot)

unit mile: Length = 1609.344 meter
assert_eq(1 mile, 1760 yard, 0.001 yard)

### Short versions of units (TODO)

unit m = meter
unit s = second
unit kg = kilogram
unit Hz = hertz
unit N = newton
unit J = joule
unit W = watt
unit Pa = pascal
unit A = ampere
unit C = coulomb
unit V = volt
unit F = farad
unit Ω = ohm
unit K = kelvin

unit rad = radian
unit deg = degree

unit min = minute

unit mph = mile/hour

### Physical constants

let gravity = 9.80665 meter / second^2
let speed_of_light = 299792458 meter / second

## Mathematical functions

# TODO: implement these as foreign functions? => benchmark
fn sqrt<D>(x: D^2) -> D = x^(1/2)
fn sqr<D>(x: D) -> D^2 = x^2
fn exp(x: Scalar) -> Scalar = e^x

# Foreign functions

fn abs<T>(x: T) -> T
fn sin(x: Scalar) -> Scalar
fn atan2<T>(y: T, x: T) -> Scalar

### Physics functions

fn fromCelsius(t_celsius: Scalar) -> Temperature = (t_celsius + 273.15) kelvin
fn toCelsius(t_kelvin: Temperature) -> Scalar = t_kelvin / kelvin - 273.15
